buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "io.codearte.accurest:accurest-gradle-plugin:$accurestVersion"
    }
}

apply plugin: 'groovy'
apply plugin: 'accurest'

ext {
    contractsDir = file("${project.rootDir}/repository/mappings/com/ofg/twitter-places-analyzer")
    wireMockStubsOutputDirRoot = file("${project.buildDir}/production/${project.name}-stubs/")
    wireMockStubsOutputDir = new File(wireMockStubsOutputDirRoot, 'repository/mappings/')
}

configurations {
    all {
        resolutionStrategy {
            eachDependency { DependencyResolveDetails details ->
                // To prevent an accidental usage of groovy-all.jar and groovy.jar in different versions
                // all modularized Groovy jars are replaced with groovy-all.jar by default.
                if (details.requested.group == 'org.codehaus.groovy' && details.requested.name != "groovy-all") {
                    details.useTarget("org.codehaus.groovy:groovy-all:${details.requested.version}")
                }
            }
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile "org.springframework:spring-web:$springVersion"
    compile "org.springframework:spring-context-support:$springVersion"
    compile "org.codehaus.groovy:groovy-all:2.4.1"
    compile 'com.fasterxml.jackson.core:jackson-databind:2.4.4'
    compile "org.codehaus.jackson:jackson-mapper-asl:$jacksonMapper"
    compile "org.codehaus.jackson:jackson-core-asl:$jacksonMapper"
    compile 'com.jayway.jsonpath:json-path-assert:1.2.0'

    testCompile('com.github.tomakehurst:wiremock:1.53') {
        exclude group: 'org.mortbay.jetty', module: 'servlet-api'
    }
    testCompile "org.spockframework:spock-spring:0.7-groovy-2.0"
    testCompile "com.jayway.restassured:rest-assured:$restAssuredVersion"
    testCompile "com.jayway.restassured:spring-mock-mvc:$restAssuredVersion"
    testCompile "io.codearte.accurest:accurest-core:$accurestVersion"
    testCompile "javax.servlet:javax.servlet-api:3.0.1"    //provided
    testCompile "ch.qos.logback:logback-classic:1.1.2"
}

accurest {
    baseClassForTests = 'com.ofg.twitter.places.BaseMockMvcSpec'
    basePackageForTests = 'accurest'
    contractsDslDir = contractsDir
//    generatedTestSourcesDir = file("${project.rootDir}/src/test/groovy/")
    stubsOutputDir = wireMockStubsOutputDir
}

//TODO: Put it into the plugin
task createWireMockStubsOutputDir << {
    wireMockStubsOutputDir.mkdirs()
}

generateWireMockClientStubs.dependsOn { createWireMockStubsOutputDir }
generateAccurest.dependsOn generateWireMockClientStubs

wrapper {
    gradleVersion '2.2.1'
}
